      SUBROUTINE SOURCE(DEPTH,MODQTY,*,Xsou,MSP,US,moden)

      DIMENSION US(*), Xsou(moden,MSP)

      COMMON /N/ MINMOD, MAXMOD, HORIG, NFREQ
      COMMON /LUNIT/ MSOURC, MODOLD, MODNEW, LUPRT

      IF(DEPTH .GT. HORIG)  RETURN 1
       DZ= HORIG/FLOAT(MSP-1)
       IND= DEPTH/DZ+1
C
      INEXT= 1
      IF(IND.EQ.MSP) INEXT=0
C
      DO 2000 ID= 1, MODQTY
        US(ID)= Xsou(id,IND)+(DEPTH/DZ-FLOAT(IND-1))*
     &   (Xsou(id,IND+INEXT)-Xsou(id,IND))
2000  CONTINUE
      RETURN
      END


      SUBROUTINE RECEIV(DEPTH,MODQTY,NP,NP1,NP2,*,RNG,
     & NRANGE,MSP,UROLD,URNEW,XT1,XT2,MODEN,DELR,ISP1,NSECT)

      DIMENSION XT1(moden,MSP), XT2(moden,MSP)
      DIMENSION RNG(*)
      DIMENSION URNEW(2,MODEN), UROLD(2,MODEN)

      COMMON /NRNGD/ HNEW, HOLD, RSTART, REND, SLNGTH, IND1


C     CALCULATION OF WATER DEPTH ASSOCIATED WITH RNG(NP1)
C
1200  CONTINUE
c      write(*,*)'HNEW, HOLD, RSTART, REND, SLNGTH, IND1'
c      write(*,*)HNEW, HOLD, RSTART, REND, SLNGTH, IND1
      HNP1= (HNEW-HOLD)*(RNG(NP1)-RSTART)/(REND-RSTART)+HOLD
      DZ= HNP1/FLOAT(MSP-1)
      IND1= DEPTH/DZ+1
C
      IF(HOLD-HNEW)2000,2400,2800
C
C   DOWN SLOPING SECTOR
C
2000  CONTINUE
      IF(HNP1.LT.DEPTH)    GOTO 2200
C   CORRECTION FOR THE SPECIAL CASE WHERE THE MODE FUNCTION IS                 
C   SAMPLED EXACTLY AT THE RECEIVER DEPTH
C
      IF(ABS(DEPTH-(IND1-1)*DZ).LT.1.0E-3*DZ) IND1= NINT(DEPTH/DZ)
C
C   CALCULATION OF THE WATER COLUMN DEPTH EXISTING AT THE POINT
C   WHERE THE LINE 'Y=RECEIVER DEPTH' HITS THE LINE
C   CONNECTING THE TWO WATER DEPTHS ASSOCIATED WITH THE
C   INDEX 'IND1'
C
      IF(IND1 .GT. 1)THEN
       HNP2= (MSP-1)*DEPTH/(IND1-1)
      ELSE
       HNP2= HNEW
      END IF
      IND2= IND1+1
      GOTO 3000
C   DEFINITION OF FIRST CALCULATABLE RANGE
2200  CONTINUE
      RNP1= (REND-RSTART)*(DEPTH-HOLD)/(HNEW-HOLD)+RSTART
      TEMPN= (NP-1)/(RNG(NP)-RNG(1))*(RNP1-RNG(1))+1
      IF(AMOD(TEMPN,1.0).EQ.0.0)  TEMPN= TEMPN-1
      NP1= TEMPN+1
      RNG(NRANGE-1)= NP1
      IF(NP1.LE.NP)    GOTO 1200
2300  NP2= NP1-1
      RETURN 1
C
C   CONSTANT DEPTH SECTOR
C
2400  CONTINUE
      IF(HNP1-DEPTH) 2300,2500,2600
2500  IND2= IND1
      GOTO 2700
2600  IND2= IND1+1
2700  NP2= NP
      CALL RECVRN(IND1,IND2,MODQTY,ISP1,NSECT,MSP,UROLD,
     & URNEW,XT1,XT2,MODEN)
      RETURN
C
C   UP SLOPING SECTOR
C
2800  CONTINUE
      IF(HNP1-DEPTH) 2300,3100,2900
2900  IND2= IND1+1
C
C   CALCULATION OF THE WATER COLUMN DEPTH EXISTING AT THE
C   POINT WHERE THE LINE 'Y=RECEIVER DEPTH' HITS THE LINE
C   CONNECTING THE TWO WATER DEPTHS ASSOCIATED WITH THE
C   INDEX 'IND2'.
C
      HNP2= (MSP-1)*(DEPTH/(IND2-1))
C
C   CALCULATION OF THE RANGE ASSOCIATED WITH HNP2
3000  CONTINUE
      RNP2= (REND-RSTART)*(HNP2-HOLD)/(HNEW-HOLD)+RSTART
      IF(RNP2 .GT. RNG(NP))  RNP2= RNG(NP)
      if (delr.eq.0) then
        NP2=NP1
      else
        NP2= (RNP2-RNG(NP1))/DELR
        NP2= MAX(0,NP2)+NP1
      endif
      CALL RECVRN(IND1,IND2,MODQTY,ISP1,NSECT,MSP,UROLD,
     & URNEW,XT1,XT2,MODEN)
      RETURN
3100  NP2= NP1
      IND2= IND1
      CALL RECVRN(IND1,IND2,MODQTY,ISP1,NSECT,MSP,UROLD,
     & URNEW,XT1,XT2,MODEN)
      RETURN
      END

      SUBROUTINE RECVRN(IND1,IND2T,MODQTY,ISP1,NSECT,
     & MSP,UROLD,URNEW,XT1,XT2,MODEN)

      DIMENSION UROLD(2,MODEN), URNEW(2,MODEN)
      DIMENSION XT1(moden,MSP), XT2(moden,MSP)

      COMMON /LUNIT/ MSOURC, MODOLD, MODNEW, LUPRT
C
C   FILLING OF UROLD AND URNEW ARRAYS
C
      IND2=MIN0(MSP,IND2T)
      IF(ISP1 .GT. NSECT)   GOTO 5000
      DO 4000  K= 1, modqty
        UROLD(1,k)= XT1(K,IND1)
        UROLD(2,k)= XT1(K,IND2)
        URNEW(1,k)= XT2(K,IND1)
        URNEW(2,k)= XT2(K,IND2)
4000  CONTINUE
      RETURN

5000  CONTINUE
      DO 6000 K= 1, modqty
          UROLD(1,k)= XT1(K,IND1)
          UROLD(2,k)= XT1(K,IND2)
          URNEW(1,k)= XT1(K,IND1)
          URNEW(2,k)= XT1(K,IND2)
6000  CONTINUE
      RETURN
      END
